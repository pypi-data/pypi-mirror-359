10 REM === FLEXAO SIMPLES: SECAO CIRCULAR CHEIA OU VAZADA ===
20 DIM A1(5),A2(5),A3(5)
30 PI=3.1416
40 INPUT "X/R INICIAL               =";X
50 INPUT "INCREMENTO EM X/R         =";X1
60 INPUT "NO. DE DIVISOES           =";E
70 INPUT "FCK                (MN/m2) =";F
80 INPUT "FYK                (MN/m2) =";F1
90 INPUT "COEF. MINORACAO CONC.     =";F2
100 INPUT "COEF. MINORACAO ACO      =";F3
110 INPUT "COEF. MAJORACAO CARGA    =";F4
120 F=F*0.85/(F2*100):F1=F1/(F3*210)
130 INPUT "DIAMETRO EXTERNO (cm)    =";D
140 INPUT "ESPESSURA PAREDE (cm)    =";E1
150 INPUT "COBRIMENTO (cm)          =";C
160 R=D/2:D1=(R-E1)/R:D2=(R-C)/R
170 INPUT "AREA DE ACO (cm2)        =";A
180 A1=(D2^2-A/(PI*R^2))^0.5
190 B=1:B1=(B+D2)*7/27:B2=2*PI/E
200 V=0
210 REM -------- PROCESSAMENTO ---------
220 IF B1>=X THEN GOTO 250
230 IF 2>=X THEN GOTO 260
240 E1=2*X/(X-6/7):GOTO 270
250 E1=10*X/(1+D2-X):GOTO 270
260 E1=3.5
270 FOR J=1 TO E
280 K1=(B2+SIN(B4)-SIN(B2+B4))/2
290 K2=(SIN((B2+B4)/2)^3-SIN(B4/2)^3)*2/(3*K1)
300 B4=B4+B2
310 A1(2)=A1:A1(3)=D2:A1(4)=D1:A1(5)=B:G=4
320 FOR I=2 TO 5
330 B5=E1*(1+(A1(I)*K2-1)/X)
340 IF 2>=G THEN GOTO 400
350 IF F1<B5 THEN GOTO 380
360 IF -F1>=B5 THEN GOTO 390
370 K3=2.1*B5:GOTO 450
380 K3=2.1*F1:GOTO 450
390 K3=-2.1*F1:GOTO 450
400 IF 0>=B5 THEN GOTO 430
410 IF 2<B5 THEN GOTO 440
420 K3=B5-B5^2/4:GOTO 450
430 K3=0:GOTO 450
440 K3=1
450 A2(I)=K1*K3+A2(I)
460 A3(I)=K1*K3*K2+A3(I):G=G-1
470 NEXT I
480 NEXT J
490 A2(5)=F*A2(5):A2(4)=F*A2(4):A3(5)=F*A3(5):A3(4)=F*A3(4)
500 FOR I=2 TO 5
510 A2(I)=A2(I)*A1(I)^2
520 A3(I)=A3(I)*A1(I)^3
530 NEXT I
540 N=(A2(5)-A2(4)+A2(3)-A2(2))*R^2/(0.1*F4)
550 IF V<>0 THEN GOTO 570
560 V=N:GOTO 670
570 K=(V/N)/ABS(V/N)
580 IF K>0 THEN GOTO 600
590 X1=X1/2:U=ABS(V-N):IF U<5 THEN GOTO 620
600 V=N:Q=V/ABS(V):X=X-(Q*X1)
610 GOTO 680
620 M=(A3(5)-A3(4)+A3(3)-A3(2))*R^3/(F4*10)
630 PRINT "-----------------"
640 PRINT "X =";X*R;" (cm)"
650 PRINT "N =";N;" (KN)"
660 PRINT "m =";M;" (KN.M)"
670 X=X+X1
680 FOR I=2 TO 5
690 A2(I)=0
700 A3(I)=0
710 NEXT I
720 GOTO 210
730 END
